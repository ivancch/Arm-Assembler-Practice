; Объявить кодовую секцию MyCode
			AREA MyCode, CODE, ReadOnly
; Объявить точку входа в программу приложения	
				ENTRY
; Объвить точку входа глобальной переменной
			EXPORT IndirectAddressing
IndirectAddressing
; Демонстрация основ косвенной адресации данных
; Загрузить регистр-указатель r0 адресом 
; расположения данных Const_0
			ADR r0,Const_0
; Загрузить слово по этому адресу в регистр r5
			LDR r5, [r0]
; Выполнить косвенную загрузку еще трех слов
; в регистры r6,r7,r8
			ADR r0,Const_1
			LDR r6, [r0]
			ADR r0,Const_2
			LDR r7, [r0]
			ADR r0,Const_3
			LDR r8, [r0]		
; Загрузить регистр-указатель r0 адресом 
; расположения данных Const_0
; Затем загрузить слово по этому адресу в регистр r5
; Повторить всё то же самое но передвигая "каретку"
			ADR r0, Const_0
			LDR r1, [r0]
			ADD r0, #4
			LDR r2, [r0]
			ADD r0, #4
			LDR r3, [r0]
			ADD r0, #4
			LDR r4, [r0]
; То же самое, но только без команды ADD
; А с поставто-инкрементированием указателя
			ADR r0, Const_0
			LDR r9,  [r0], #4
			LDR r10, [r0], #4
			LDR r11, [r0], #4
			LDR r12, [r0]

; Задача извлечь из массива слов в кодовой памяти
; вторую и четвертую константы и найти их
; сумму с записью результата в регистр r5
WORD 		EQU 4
			ADR r0, Const_0
			LDR r1, [r0, #WORD*1]
			LDR r2, [r0, #WORD*3]
			ADD r5, r1, r2
			BX LR
		
			ALIGN
; Инициализация констант в кодовой памяти
Const_0		DCD 0x7788FFAA
Const_1 	DCD 0x22221111
Const_2 	DCW 34,35
Const_3 	DCB 1,2,3,4
; Конец ассемблерного текста
			END